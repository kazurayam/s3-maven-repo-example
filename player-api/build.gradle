import com.amazonaws.auth.profile.ProfileCredentialsProvider
import com.amazonaws.services.s3.model.ObjectMetadata

/**
 * player-api/build.gradle
 */
buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.amazonaws:aws-java-sdk-core:1.11.5'
    }
}

plugins {
    id 'java'
    id 'maven-publish'
    id 'jp.classmethod.aws' version '0.41'
    id 'jp.classmethod.aws.s3' version '0.41'
}

ext {
    awsCredentials = new ProfileCredentialsProvider().credentials
}

group = 'com.jacobaseverson.player'
jar.archiveBaseName = 'player-api'
jar.archiveVersion = '0.0.1'

repositories {
    mavenLocal()
    jcenter()
}

dependencies {
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.8.4'

    // https://mvnrepository.com/artifact/com.amazonaws/aws-lambda-java-core
    implementation group: 'com.amazonaws', name: 'aws-lambda-java-core', version: '1.2.0'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    repositories {
        maven {
            url "s3://${repoBucketName}/releases"
            credentials(AwsCredentials) {
                accessKey awsCredentials.AWSAccessKeyId
                secretKey awsCredentials.AWSSecretKey
            }
        }
    }
}

task uploadArtifact(type: jp.classmethod.aws.gradle.s3.AmazonS3FileUploadTask) {
    bucketName "${artifactsBucketName}"
    file file("${buildDir}/libs/player-api-0.0.1.jar")
    key "player-api-0.0.1.jar"
    def m = new ObjectMetadata()
    m.setCacheControl("no-cache, no-store")
    objectMetadata = m
}

task transferArtifact {

}